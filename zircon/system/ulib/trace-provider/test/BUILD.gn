# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

group("test") {
  testonly = true
  deps = [ ":provider-test" ]
}

test("provider-test") {
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  visibility = [ ":*" ]
  output_name = "trace-provider-test"
  sources = [
    "../fnv1hash.h",
    "../session.h",
    "fake_trace_manager.cc",
    "fake_trace_manager.h",
    "provider_test.cc",
    "session_test.cc",
  ]
  deps = [
    "//sdk/fidl/fuchsia.tracing.provider:fuchsia.tracing.provider_c",
    "//sdk/lib/fdio",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fidl",
    "//zircon/system/ulib/fidl:fidl_base",
    "//zircon/system/ulib/trace",
    "//zircon/system/ulib/trace-provider",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("provider-test-pkg") {
  package_name = "trace-provider-test"
  deps = [ ":provider-test" ]
}

group("tests") {
  testonly = true
  deps = [ ":provider-test-pkg" ]
}
