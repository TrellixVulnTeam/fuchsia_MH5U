# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/zircon/zx_library.gni")

expect_includes("client_includes") {
  includes = [ "meta/client.shard.cml" ]
}

zx_library("devmgr-integration-test") {
  testonly = true
  sdk = "shared"
  sdk_headers = [ "lib/devmgr-integration-test/fixture.h" ]

  sources = [ "launcher.cc" ]
  public_deps = [
    ":client_includes",

    # <lib/devmgr-integration-test/fixture.h> has #include <lib/vfs/cpp/pseudo_dir.h>
    "//sdk/lib/vfs/cpp",
    "//zircon/system/ulib/async",

    # <lib/devmgr-integration-test/fixture.h> has #include <lib/devmgr-launcher/launch.h>.
    "//zircon/system/ulib/devmgr-launcher",

    # <lib/devmgr-integration-test/fixture.h> has #include <fbl/unique_fd.h>.
    "//zircon/system/ulib/fbl",

    # <lib/devmgr-integration-test/fixture.h> has #include <lib/fit/function.h>.
    "//sdk/lib/fit",

    # <lib/devmgr-integration-test/fixture.h> has #include <lib/zx/channel.h>.
    "//zircon/system/ulib/zx",

    # <lib/devmgr-integration-test/fixture.h> has #include <fidl/fuchsia.io/cpp/wire.h>
    "//sdk/fidl/fuchsia.io:fuchsia.io_llcpp",

    # <lib/devmgr-integration-test/fixture.h> has #include <fidl/fuchsia.process.lifecycle/cpp/wire.h>
    "//sdk/fidl/fuchsia.process.lifecycle:fuchsia.process.lifecycle_llcpp",
  ]
  deps = [
    "//sdk/fidl/fuchsia.boot:fuchsia.boot_c",
    "//sdk/fidl/fuchsia.boot:fuchsia.boot_llcpp",
    "//sdk/fidl/fuchsia.component:fuchsia.component_llcpp",
    "//sdk/fidl/fuchsia.driver.framework:fuchsia.driver.framework_llcpp",
    "//sdk/fidl/fuchsia.exception:fuchsia.exception_llcpp",
    "//sdk/fidl/fuchsia.io:fuchsia.io_llcpp",
    "//sdk/fidl/fuchsia.kernel:fuchsia.kernel_c",
    "//sdk/fidl/fuchsia.power.manager:fuchsia.power.manager_llcpp",
    "//sdk/fidl/fuchsia.process:fuchsia.process_llcpp",
    "//sdk/fidl/fuchsia.scheduler:fuchsia.scheduler_c",
    "//sdk/lib/fdio",
    "//sdk/lib/sys/cpp",
    "//src/lib/ddk",
    "//src/lib/storage/vfs/cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/devmgr-launcher",
    "//zircon/system/ulib/fdio-caller",
    "//zircon/system/ulib/fidl-async",
    "//zircon/system/ulib/fidl-async:fidl-async-cpp",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/mock-boot-arguments",
    "//zircon/system/ulib/service:service-llcpp",
    "//zircon/system/ulib/syslog",
    "//zircon/system/ulib/zx",
  ]

  # These binaries need to get launched by devmgr-integration-test.
  deps += [
    "//src/devices/bin/driver-index:component",
    "//src/devices/bin/driver_host",
    "//src/devices/bin/driver_manager",
    "//src/storage/fshost",
  ]

  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]

  # STOP: Do not add any new entries to this allow list. Use //sdk/lib/driver_test_realm instead.
  visibility = [
    "//src/lib/isolated_devmgr/*",
    "//vendor/*",
    "//zircon/system/ulib/devmgr-integration-test/test/*",
    "//zircon/system/ulib/driver-integration-test/*",
  ]
}

group("tests") {
  testonly = true
  deps = [ "test:devmgr-integration-test" ]
}
