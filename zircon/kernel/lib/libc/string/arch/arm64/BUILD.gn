# Copyright 2019 The Fuchsia Authors
#
# Use of this source code is governed by a MIT-style
# license that can be found in the LICENSE file or at
# https://opensource.org/licenses/MIT

source_set("arm64") {
  if (toolchain_variant.tags + [ "strict-align" ] - [ "strict-align" ] !=
      toolchain_variant.tags) {
    # This environment can't handled unaligned accesses, which the
    # optimized assembly routines do.
    sources = [
      "//zircon/kernel/lib/libc/string/memcpy.c",
      "//zircon/kernel/lib/libc/string/memset.c",
    ]
    deps = [ "//zircon/kernel/lib/libc:headers" ]

    # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
    configs += [ "//build/config:Wno-conversion" ]
  } else {
    # These use '#include "third_party/lib/cortex-strings/src/aarch64/..."'.
    # and '#include "third_party/lib/cortex-strings/no-neon/src/aarch64/..."'.
    include_dirs = [ "//zircon/" ]
    sources = [
      "memcpy.S",
      "memset.S",
    ]
  }
}
