# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_test.gni")

rustc_test("driver") {
  name = "component_launching_test"
  edition = "2018"

  deps = [
    "//src/lib/fdio/rust:fdio",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/testing/fidl:placeholders-rustc",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tempfile",
  ]

  sources = [ "src/lib.rs" ]
}

rustc_binary("echo_service_checker_bin") {
  name = "echo_service_checker"
  edition = "2018"
  source_root = "src/echo_service_checker.rs"
  testonly = true

  deps = [
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/testing/fidl:placeholders-rustc",
    "//third_party/rust_crates:anyhow",
  ]

  sources = [ "src/echo_service_checker.rs" ]
}

rustc_binary("echo_service_checker_with_probe_bin") {
  name = "echo_service_checker_with_probe"
  edition = "2018"
  source_root = "src/echo_service_checker_with_probe.rs"
  testonly = true

  deps = [
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/testing/fidl:placeholders-rustc",
    "//third_party/rust_crates:anyhow",
  ]

  sources = [ "src/echo_service_checker_with_probe.rs" ]
}

rustc_binary("injected_directory_checker_bin") {
  name = "injected_directory_checker"
  edition = "2018"
  source_root = "src/injected_directory_checker.rs"
  testonly = true

  sources = [ "src/injected_directory_checker.rs" ]
}

rustc_binary("stdio_writer_bin") {
  name = "stdio_writer"
  edition = "2018"
  source_root = "src/stdio_writer.rs"
  testonly = true

  sources = [ "src/stdio_writer.rs" ]
}

fuchsia_component("echo-service-checker") {
  deps = [ ":echo_service_checker_bin" ]
  manifest = "meta/echo-service-checker.cmx"
  testonly = true
}

fuchsia_component("echo-service-checker-with-probe") {
  deps = [ ":echo_service_checker_with_probe_bin" ]
  manifest = "meta/echo-service-checker-with-probe.cmx"
  testonly = true
}

fuchsia_component("injected-directory-checker") {
  deps = [ ":injected_directory_checker_bin" ]
  manifest = "meta/injected-directory-checker.cmx"
  testonly = true
}

fuchsia_component("stdio-writer") {
  deps = [ ":stdio_writer_bin" ]
  manifest = "meta/stdio-writer.cmx"
  testonly = true
}

fuchsia_unittest_component("component-launching-test") {
  manifest = "meta/component-launching-test.cmx"
  deps = [ ":driver" ]
}

fuchsia_test_package("component-launching-tests") {
  test_components = [ ":component-launching-test" ]
  deps = [
    ":echo-service-checker",
    ":echo-service-checker-with-probe",
    ":injected-directory-checker",
    ":stdio-writer",
  ]
}
