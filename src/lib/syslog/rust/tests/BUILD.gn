# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_test.gni")

group("tests") {
  testonly = true
  deps = [ ":fuchsia-syslog-integration-tests" ]
}

fuchsia_component("panic_is_logged") {
  deps = [ ":panic_is_logged_bin" ]
  manifest = "meta/panic_is_logged.cmx"
  testonly = true
}

fuchsia_component("panicker") {
  deps = [ ":panicker_bin" ]
  manifest = "meta/panicker.cmx"
}

rustc_test("panic_is_logged_bin") {
  name = "panic_is_logged"
  edition = "2018"
  source_root = "panic_is_logged.rs"
  deps = [
    "//sdk/fidl/fuchsia.logger:fuchsia.logger-rustc",
    "//sdk/fidl/fuchsia.sys:fuchsia.sys-rustc",
    "//src/diagnostics/lib/validating-log-listener",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
  ]

  sources = [ "panic_is_logged.rs" ]
}

rustc_binary("panicker_bin") {
  name = "panicker"
  edition = "2018"
  source_root = "panicker.rs"
  deps = [ "//src/lib/syslog/rust:syslog" ]

  sources = [ "panicker.rs" ]
}

fuchsia_test_package("fuchsia-syslog-integration-tests") {
  test_components = [ ":panic_is_logged" ]
  deps = [ ":panicker" ]
}
