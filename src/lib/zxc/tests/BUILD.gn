# Copyright 2020 The Fuchsia Authors
#
# Use of this source code is governed by a MIT-style
# license that can be found in the LICENSE file or at
# https://opensource.org/licenses/MIT

import("//build/components.gni")
import("//build/test.gni")
import("//build/testing/environments.gni")

group("tests") {
  testonly = true
  deps = [
    ":zxc-tests",
    ":zxc_unittests($host_toolchain)",
  ]
}

test("zxc_unittests") {
  testonly = true
  output_name = "zxc_unittests"
  sources = [ "result_tests.cc" ]
  deps = [
    "//zircon/system/ulib/zxc",
    "//zircon/system/ulib/zxtest",
  ]

  if (is_fuchsia) {
    deps += [ "//sdk/lib/fdio" ]
  }
}

fuchsia_unittest_package("zxc-tests") {
  deps = [ ":zxc_unittests" ]
  test_specs = {
    environments = [ emu_env ]
  }
}
