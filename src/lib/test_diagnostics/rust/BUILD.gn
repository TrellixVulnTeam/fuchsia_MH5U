# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_library.gni")

lib_sources = [
  "src/diagnostics.rs",
  "src/lib.rs",
]

lib_deps = [
  "//sdk/fidl/fuchsia.developer.remotecontrol:fuchsia.developer.remotecontrol-rustc",
  "//sdk/fidl/fuchsia.sys:fuchsia.sys-rustc",
  "//sdk/fidl/fuchsia.test:fuchsia.test-rustc",
  "//sdk/fidl/fuchsia.test.manager:fuchsia.test.manager-rustc",
  "//src/lib/diagnostics/data/rust",
  "//src/lib/fidl/rust/fidl",
  "//src/lib/fuchsia-async",
  "//src/lib/zircon/rust:fuchsia-zircon-status",
  "//third_party/rust_crates:anyhow",
  "//third_party/rust_crates:futures",
  "//third_party/rust_crates:glob",
  "//third_party/rust_crates:log",
  "//third_party/rust_crates:pin-project",
  "//third_party/rust_crates:serde_json",
  "//third_party/rust_crates:thiserror",
]

shared_test_deps = [
  "//third_party/rust_crates:assert_matches",
  "//third_party/rust_crates:maplit",
  "//third_party/rust_crates:pretty_assertions",
]

rustc_library("test_diagnostics") {
  with_unit_tests = true
  edition = "2018"

  deps = lib_deps

  if (is_fuchsia) {
    deps += [
      "//sdk/fidl/fuchsia.diagnostics:fuchsia.diagnostics-rustc",
      "//src/lib/diagnostics/reader/rust",
      "//src/lib/fuchsia-component",
    ]
  }

  test_deps = shared_test_deps + [
                "//sdk/fidl/fuchsia.mem:fuchsia.mem-rustc",
                "//src/lib/zircon/rust:fuchsia-zircon",
              ]

  sources = lib_sources
}

if (!is_fuchsia) {
  rustc_test("test_diagnostics_host_unittests") {
    sources = lib_sources
    deps = shared_test_deps + lib_deps
  }
}

fuchsia_unittest_package("test_diagnostics_unittests") {
  deps = [ ":test_diagnostics_test" ]
}

group("tests") {
  testonly = true
  deps = [
    ":test_diagnostics_host_unittests($host_toolchain)",
    ":test_diagnostics_unittests",
  ]
}
