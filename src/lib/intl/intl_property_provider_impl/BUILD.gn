# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fidl/fidl.gni")

group("tests") {
  testonly = true
  deps = [ ":intl_property_provider_impl_tests" ]
}

fidl("fidl") {
  name = "fuchsia.intl.merge"
  visibility = [ ":*" ]
  sources = [ "merge.fidl" ]
  public_deps = [
    "//sdk/fidl/fuchsia.intl",
    "//sdk/fidl/fuchsia.settings",
  ]
}

source_set("intl_property_provider_impl") {
  sources = [
    "intl_property_provider_impl.cc",
    "intl_property_provider_impl.h",
  ]

  deps = [
    ":locale_util",
    "//sdk/fidl/fuchsia.intl",
    "//sdk/fidl/fuchsia.settings",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/service/cpp",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fostr/fidl/fuchsia.intl",
    "//src/lib/fostr/fidl/fuchsia.settings",
    "//src/lib/fxl",
    "//src/lib/icu_data/cpp",
    "//third_party/icu",
  ]

  public_deps = [
    ":fidl",
    "//sdk/fidl/fuchsia.intl",
    "//sdk/fidl/fuchsia.settings",
    "//sdk/lib/fit-promise",
  ]
}

executable("intl_property_provider_impl_test_bin") {
  testonly = true
  output_name = "intl_property_provider_impl_test"

  sources = [ "intl_property_provider_impl_test.cc" ]

  deps = [
    ":intl_property_provider_impl",
    "//garnet/bin/trace/tests:component_context",
    "//sdk/fidl/fuchsia.sys",
    "//sdk/lib/sys/cpp/testing:unit",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fidl_fuchsia_intl_ext/cpp",
    "//src/lib/fostr/fidl/fuchsia.intl",
    "//src/lib/fostr/fidl/fuchsia.settings",
    "//src/lib/fxl/test:test_settings",
    "//src/lib/testing/loop_fixture",
    "//src/modular/lib/fidl:clone",
  ]
}

source_set("locale_util") {
  visibility = [ ":*" ]

  sources = [
    "locale_util.cc",
    "locale_util.h",
  ]

  deps = [
    "//sdk/lib/syslog/cpp",
    "//src/lib/fostr/fidl/fuchsia.intl",
    "//src/lib/fxl",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.intl",
    "//sdk/lib/fit-promise",
    "//third_party/icu",
  ]
}

executable("locale_util_unit_test_bin") {
  testonly = true
  output_name = "locale_util_unit_test"

  sources = [ "locale_util_unit_test.cc" ]

  deps = [
    ":locale_util",
    "//sdk/fidl/fuchsia.intl",
    "//src/lib/fidl_fuchsia_intl_ext/cpp",
    "//src/lib/fostr/fidl/fuchsia.intl",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/icu_data/cpp",
    "//third_party/icu",
  ]
}

fuchsia_unittest_component("intl_property_provider_impl_test") {
  deps = [ ":intl_property_provider_impl_test_bin" ]
}

fuchsia_unittest_component("locale_util_unit_test") {
  deps = [ ":locale_util_unit_test_bin" ]
}

fuchsia_test_package("intl_property_provider_impl_tests") {
  test_components = [
    ":intl_property_provider_impl_test",
    ":locale_util_unit_test",
  ]
  deps = [ "//src/intl:icudtl" ]
}
