# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("../../../gn/build_rules.gni")

config("public_config") {
  # Clients should include "tests/common/svg/<header>"
  include_dirs = [ graphics_compute_dir ]
}

source_set("svg") {
  public = [
    "scoped_svg.h",
    "svg_bounds.h",
    "svg_path_sink.h",
    "svg_print.h",
    "svg_scene.h",
    "svg_utils.h",
  ]
  sources = [
    "scoped_svg.h",
    "svg_bounds.cc",
    "svg_bounds.h",
    "svg_path_sink.cc",
    "svg_path_sink.h",
    "svg_print.cc",
    "svg_print.h",
    "svg_scene.cc",
    "svg_scene.h",
    "svg_utils.cc",
    "svg_utils.h",
  ]
  public_deps = [
    "${graphics_compute_dir}/svg:svg",
    "${graphics_compute_dir}/tests:common",
  ]
  public_configs = [ ":public_config" ]
  configs += [
    # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
    "//build/config:Wno-conversion",
  ]
}

executable("spn_svg_dump") {
  sources = [ "svg_dump_main.cc" ]
  deps = [ ":svg" ]
}

graphics_compute_unittests("unittests") {
  sources = [
    "scoped_svg_unittest.cc",
    "svg_bounds_unittest.cc",
    "svg_print_unittest.cc",
    "svg_scene_unittest.cc",
    "svg_utils_unittest.cc",
  ]
  deps = [
    ":svg",
    "${graphics_compute_dir}/tests:common_test_utils",
  ]
  configs = [
    # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
    "//build/config:Wno-conversion",
  ]
}
