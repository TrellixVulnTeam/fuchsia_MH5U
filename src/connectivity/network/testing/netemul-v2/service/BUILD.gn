# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fidl/fidl.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")
import("//build/rust/rustc_test.gni")

rustc_binary("bin") {
  testonly = true
  with_unit_tests = true
  name = "netemul_sandbox_v2"
  edition = "2018"
  deps = [
    "//sdk/fidl/fuchsia.component:fuchsia.component-rustc",
    "//sdk/fidl/fuchsia.component.test:fuchsia.component.test-rustc",
    "//sdk/fidl/fuchsia.data:fuchsia.data-rustc",
    "//sdk/fidl/fuchsia.driver.test:fuchsia.driver.test-rustc",
    "//sdk/fidl/fuchsia.io:fuchsia.io-rustc",
    "//sdk/fidl/fuchsia.logger:fuchsia.logger-rustc",
    "//sdk/fidl/fuchsia.net.tun:fuchsia.net.tun-rustc",
    "//sdk/fidl/fuchsia.sys2:fuchsia.sys2-rustc",
    "//sdk/lib/driver_test_realm/realm_builder/rust",
    "//src/connectivity/network/testing/netemul-v2/fidl:fuchsia.netemul-rustc",
    "//src/connectivity/network/testing/netemul/lib/fidl:network-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-component-test",
    "//src/lib/storage/vfs/rust:vfs",
    "//src/lib/syslog/rust:syslog",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/sys/lib/cm_rust",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:assert_matches",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:thiserror",
  ]
  test_deps = [
    "//garnet/lib/rust/files_async",
    "//sdk/fidl/fuchsia.device:fuchsia.device-rustc",
    "//src/connectivity/network/testing/netemul-v2/test-support:fidl-rustc",
    "//src/lib/diagnostics/data/rust",
    "//src/lib/diagnostics/hierarchy/rust",
    "//src/lib/diagnostics/reader/rust",
    "//src/lib/diagnostics/selectors",
    "//src/lib/fuchsia",
    "//src/lib/storage/fuchsia-vfs-watcher",
    "//src/lib/testing/fixture",
    "//third_party/rust_crates:assert_matches",
  ]
  sources = [ "src/main.rs" ]
  configs -= [ "//build/config/rust:allow_unused_results" ]
}

group("netemul-driver-test-realm") {
  testonly = true
  public_deps = [
    "//src/connectivity/ethernet/drivers/ethernet",
    "//src/connectivity/ethernet/drivers/ethertap",
    "//src/devices/misc/drivers/test-parent",
  ]
}

fuchsia_component("netemul-sandbox") {
  testonly = true
  component_name = "netemul-sandbox-v2"
  manifest = "meta/netemul-sandbox-v2.cml"
  deps = [
    ":bin",
    ":netemul-driver-test-realm",
    "//src/connectivity/network/testing/netemul-v2/network-context",
    "//src/connectivity/network/tun/network-tun:component-v2",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":netemul-v2-tests" ]
}

fuchsia_test_component("netemul-sandbox-v2-tests") {
  manifest = "meta/netemul-sandbox-v2-tests.cml"
  deps = [
    ":bin_test",
    ":netemul-driver-test-realm",
    "//src/connectivity/network/testing/netemul-v2/network-context",
    "//src/connectivity/network/testing/netemul-v2/test-support:counter",
    "//src/connectivity/network/testing/netemul-v2/test-support:counter-without-program",
    "//src/connectivity/network/testing/netemul-v2/test-support:devfs-subdir-user",
    "//src/connectivity/network/tun/network-tun:component-v2",
  ]
}

fuchsia_test_package("netemul-v2-tests") {
  test_components = [ ":netemul-sandbox-v2-tests" ]
  test_specs = {
    # TODO(https://fxbug.dev/82295): run tests in parallel when this doesn't
    # cause embedded archivist to crash.
    parallel = 1
  }
}
