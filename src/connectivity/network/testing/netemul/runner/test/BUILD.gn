# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

group("test") {
  testonly = true
  deps = [
    ":netemul-guest-test",
    ":netemul-sandbox-test",
    ":sandbox-service_spec",
    ":sandbox-unittest_spec",
  ]
}

fuchsia_test_component("dynamic-devices-test") {
  manifest = "meta/dynamic-devices-test.cmx"
  deps = [ "dynamic_devices" ]
}

fuchsia_test_component("easy-netstack-cfg-test") {
  manifest = "meta/easy-netstack-cfg-test.cmx"
  deps = [ "easy_netstack_cfg" ]
}

fuchsia_test_component("easy-netstack-cfg-netdevice-test") {
  manifest = "meta/easy-netstack-cfg-netdevice-test.cmx"
  deps = [ "easy_netstack_cfg" ]
}

fuchsia_test_component("env-build-test") {
  manifest = "meta/env-build-test.cmx"
  deps = [ "env_build" ]
}

fuchsia_test_component("inheritance-test") {
  manifest = "meta/inheritance-test.cmx"
  deps = [ "inheritance" ]
}

fuchsia_test_component("netstack-intermediary-multi-interface-test") {
  manifest = "meta/netstack-intermediary-multi-interface-test.cmx"
  deps = [ "netstack_intermediary" ]
}

fuchsia_test_component("netstack-intermediary-single-interface-test") {
  manifest = "meta/netstack-intermediary-single-interface-test.cmx"
  deps = [ "netstack_intermediary" ]
}

fuchsia_test_component("netstack-socks-test") {
  manifest = "meta/netstack-socks-test.cmx"
  deps = [ "netstack_socks" ]
}

fuchsia_test_component("svc-list-test") {
  manifest = "meta/svc-list-test.cmx"
  deps = [ "svc_list" ]
}

fuchsia_test_component("virtual-device-directory-test") {
  manifest = "meta/virtual-device-directory-test.cmx"
  deps = [ "virtual_device_directory" ]
}

fuchsia_test_component("network-device-test") {
  manifest = "meta/network-device-test.cmx"
  deps = [ "network_device" ]
}

fuchsia_test_component("sandbox-service-test-legacy") {
  manifest = "meta/sandbox-service-test.cmx"
  component_name = "sandbox-service-test"
  deps = [ "sandbox_service" ]
}

fuchsia_test_component("sandbox-service-test") {
  manifest = "meta/sandbox-service-test.cml"
  deps = [ ":sandbox-service-test-legacy" ]
}

fuchsia_test("sandbox-service_spec") {
  package = ":netemul-sandbox-test"
  component = ":sandbox-service-test"

  log_settings = {
    max_severity = "ERROR"
  }
}

fuchsia_test_component("sandbox-unittest-legacy") {
  manifest = "meta/sandbox-unittest.cmx"
  component_name = "sandbox-unittest"
  deps =
      [ "//src/connectivity/network/testing/netemul/runner:sandbox_unittest" ]
}

fuchsia_test_component("sandbox-unittest") {
  manifest = "meta/sandbox-unittest.cml"
  deps = [ ":sandbox-unittest-legacy" ]
}

fuchsia_test("sandbox-unittest_spec") {
  package = ":netemul-sandbox-test"
  component = ":sandbox-unittest"

  log_settings = {
    max_severity = "ERROR"
  }
}

fuchsia_test_component("mock-device-settings-test") {
  manifest = "meta/mock-device-settings-test.cmx"
  deps = [ "//src/connectivity/network/testing/netemul/runner/helpers/mock_device_settings:mock_device_settings_test" ]
}

fuchsia_test_component("model-unittest") {
  manifest = "meta/model-unittest.cmx"
  deps = [
    "//src/connectivity/network/testing/netemul/runner/model:model_unittest",
  ]
}

fuchsia_test_package("netemul-sandbox-test") {
  package_deps_for_infra_only = [
    "//src/connectivity/network/testing/netemul/runner:netemul-sandbox",
    "//src/connectivity/network/testing/netemul/runner/runner:netemul-runner",
    "//src/connectivity/network/tun/network-tun:network-tun",
    "//src/diagnostics/archivist:archivist-for-embedding",
  ]
  test_components = [
    ":dynamic-devices-test",
    ":easy-netstack-cfg-netdevice-test",
    ":easy-netstack-cfg-test",
    ":env-build-test",
    ":inheritance-test",
    ":mock-device-settings-test",
    ":model-unittest",
    ":netstack-intermediary-multi-interface-test",
    ":netstack-intermediary-single-interface-test",
    ":netstack-socks-test",
    ":network-device-test",
    ":svc-list-test",
    ":virtual-device-directory-test",
  ]
  deps = [
    ":sandbox-service-test",
    ":sandbox-unittest",
    "dummy_proc",
    "dummy_proc:dummy_proc_with_dev",
    "//src/connectivity/network/netstack:component-legacy",
    "//src/connectivity/network/testing/netemul/runner:mock-device-settings",
    "//src/connectivity/network/testing/netemul/runner:netemul-sandbox-component",
    "//src/connectivity/network/testing/netemul/runner:netstack-cfg",
    "//src/connectivity/network/testing/netemul/runner:netstack-intermediary",
    "//src/connectivity/network/tun/network-tun:component",
  ]
}

fuchsia_test_component("guest-test-legacy") {
  manifest = "meta/guest-test.cmx"
  component_name = "guest-test"
  deps = [ "guest" ]
}

fuchsia_test_component("guest-test") {
  manifest = "meta/guest-test.cml"
  deps = [ ":guest-test-legacy" ]
}

resource("test_script") {
  sources = [ "//src/virtualization/lib/guest_interaction/test/test_script.sh" ]
  outputs = [ "data/test_script.sh" ]
}

resource("setup_linux_client") {
  sources = [ "multiple_guests/data/setup_linux_client.sh" ]
  outputs = [ "data/setup_linux_client.sh" ]
}

resource("setup_linux_router") {
  sources = [ "multiple_guests/data/setup_linux_router.sh" ]
  outputs = [ "data/setup_linux_router.sh" ]
}

fuchsia_test_component("grpc-client-stress-test-legacy") {
  manifest = "meta/grpc-client-stress-test.cmx"
  component_name = "grpc-client-stress-test"
  deps = [ "guest_grpc_stress:grpc_client_stress" ]
}

fuchsia_test_component("grpc-client-stress-test") {
  manifest = "meta/grpc-client-stress-test.cml"
  deps = [ ":grpc-client-stress-test-legacy" ]
}

fuchsia_test_component("multiple-guests-test-legacy") {
  manifest = "meta/multiple-guests-test.cmx"
  component_name = "multiple-guests-test"
  deps = [ "multiple_guests" ]
}

fuchsia_test_component("multiple-guests-test") {
  manifest = "meta/multiple-guests-test.cml"
  deps = [ ":multiple-guests-test-legacy" ]
}

fuchsia_test_package("netemul-guest-test") {
  test_specs = {
    environments = guest_envs
  }
  test_components = [
    ":guest-test",
    ":multiple-guests-test",
    ":grpc-client-stress-test",
  ]
  deps = [
    ":setup_linux_client",
    ":setup_linux_router",
    ":test_script",
    "//src/connectivity/network/netstack:component-legacy",
    "//src/connectivity/network/testing/netemul/runner:netstack-cfg",
  ]
}
