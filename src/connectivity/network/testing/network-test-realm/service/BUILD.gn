# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")

rustc_binary("bin") {
  output_name = "realm_controller_server"
  edition = "2018"
  deps = [
    ":network_test_realm",
    "//garnet/lib/rust/files_async",
    "//sdk/fidl/fuchsia.component:fuchsia.component-rustc",
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl-rustc",
    "//sdk/fidl/fuchsia.hardware.ethernet:fuchsia.hardware.ethernet-rustc",
    "//sdk/fidl/fuchsia.io:fuchsia.io-rustc",
    "//sdk/fidl/fuchsia.net:fuchsia.net-rustc",
    "//sdk/fidl/fuchsia.net.debug:fuchsia.net.debug-rustc",
    "//sdk/fidl/fuchsia.net.interfaces:fuchsia.net.interfaces-rustc",
    "//sdk/fidl/fuchsia.netstack:fuchsia.netstack-rustc",
    "//sdk/fidl/fuchsia.posix.socket:fuchsia.posix.socket-rustc",
    "//src/connectivity/lib/net-types",
    "//src/connectivity/lib/ping",
    "//src/connectivity/network/testing/network-test-realm/fidl:fuchsia.net.test.realm-rustc",
    "//src/lib/async-utils",
    "//src/lib/fdio/rust:fdio",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/network/fidl_fuchsia_net_ext",
    "//src/lib/network/fidl_fuchsia_net_interfaces_ext",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:libc",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:socket2",
    "//third_party/rust_crates:thiserror",
  ]
  sources = [ "src/main.rs" ]
  configs -= [ "//build/config/rust:allow_unused_results" ]
}

rustc_library("network_test_realm") {
  deps = [
    "//sdk/fidl/fuchsia.component:fuchsia.component-rustc",
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl-rustc",
    "//sdk/fidl/fuchsia.net.interfaces:fuchsia.net.interfaces-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/network/fidl_fuchsia_net_interfaces_ext",
    "//third_party/rust_crates:anyhow",
  ]
  sources = [ "src/lib.rs" ]
  configs -= [ "//build/config/rust:allow_unused_results" ]
}

fuchsia_component("hermetic_network_v2") {
  manifest = "meta/hermetic_network_v2.cml"
  component_name = "hermetic_network_v2"
  deps = [
    "//src/connectivity/network/dhcp:component",
    "//src/connectivity/network/dhcpv6/client:component",
    "//src/connectivity/network/dns:component",
    "//src/connectivity/network/netstack:component-conformance",
  ]
}

fuchsia_component("controller") {
  component_name = "controller"
  manifest = "meta/controller.cml"
  deps = [
    ":bin",
    ":hermetic_network_v2",
  ]
}

fuchsia_package("network-test-realm") {
  package_name = "network-test-realm"
  deps = [ ":controller" ]
}
