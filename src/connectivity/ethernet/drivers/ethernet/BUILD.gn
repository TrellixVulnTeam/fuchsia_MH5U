# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("ethernet_bind") {
  rules = "ethernet.bind"
  header_output = "ethernet-bind.h"
  bind_output = "ethernet.bindbc"
  tests = "bind-tests.json"
  deps = [ "//src/devices/bind/fuchsia.ethernet" ]
}

fuchsia_driver("ethernet-driver") {
  output_name = "ethernet"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [ "ethernet.cc" ]
  deps = [
    ":ethernet_bind",
    "//sdk/banjo/fuchsia.hardware.ethernet:fuchsia.hardware.ethernet_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.ethernet:fuchsia.hardware.ethernet_llcpp",
    "//sdk/lib/fidl/cpp:cpp_base",
    "//src/devices/lib/dev-operation",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fidl",
    "//zircon/system/ulib/fidl-utils",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zircon-internal",
  ]
}

fuchsia_driver_component("ethernet") {
  component_name = "ethernet"
  deps = [ ":ethernet-driver" ]
  info = "ethernet-info.json"
  colocate = true
}

source_set("test_util") {
  sources = [ "test_util.h" ]
  testonly = true
  public_deps = [
    "//sdk/banjo/fuchsia.hardware.ethernet:fuchsia.hardware.ethernet_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.ethernet:fuchsia.hardware.ethernet_llcpp",
    "//src/devices/lib/dev-operation",
    "//src/devices/testing/fake_ddk",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fidl-utils",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/zircon-internal",
  ]
}

test("ethernet-unittest") {
  output_name = "ethernet-unittest"
  configs += [ "//build/config:all_source" ]
  sources = [
    "ethernet-test.cc",
    "ethernet.cc",
  ]
  deps = [
    ":ethernet_bind",
    ":test_util",
    "//sdk/lib/fidl/cpp:cpp_base",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("ethernet-unittest-package") {
  package_name = "ethernet-unittest"
  deps = [ ":ethernet-unittest" ]
}

test("ethernet-integration") {
  output_name = "ethernet-integration-test"
  sources = [ "ethernet-integration-test.cc" ]
  deps = [
    "//sdk/banjo/fuchsia.hardware.ethernet:fuchsia.hardware.ethernet_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.ethernet:fuchsia.hardware.ethernet_c",
    "//sdk/fidl/fuchsia.hardware.ethertap:fuchsia.hardware.ethertap_c",
    "//sdk/lib/fdio",
    "//sdk/lib/fidl/cpp:cpp_base",
    "//src/devices/testing/devmgr-integration-test-shim",
    "//src/lib/ddk",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fidl",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("ethernet-integration-test") {
  deps = [
    ":ethernet",
    ":ethernet-integration",
    "//src/connectivity/ethernet/drivers/ethertap",
    "//src/devices/misc/drivers/test-parent",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":ethernet-integration-test",
    ":ethernet-unittest-package",
    ":ethernet_bind_test",
    "netdevice-migration:tests",
  ]
}
