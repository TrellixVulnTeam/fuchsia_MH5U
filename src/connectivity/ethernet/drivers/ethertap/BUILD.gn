# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("ethertap_bind") {
  rules = "ethertap.bind"
  header_output = "ethertap-bind.h"
  tests = "bind-tests.json"
  deps = [ "//src/devices/bind/fuchsia.test" ]
}

fuchsia_driver("ethertap-driver") {
  output_name = "ethertap"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [ "ethertap.cc" ]
  deps = [
    ":ethertap_bind",
    "//sdk/banjo/fuchsia.hardware.ethernet:fuchsia.hardware.ethernet_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.ethernet:fuchsia.hardware.ethernet_c",
    "//sdk/fidl/fuchsia.hardware.ethertap:fuchsia.hardware.ethertap_c",
    "//sdk/fidl/fuchsia.hardware.ethertap:fuchsia.hardware.ethertap_llcpp",
    "//sdk/lib/fidl/cpp:cpp_base",
    "//src/devices/lib/dev-operation",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fidl",
    "//zircon/system/ulib/pretty",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_driver_component("ethertap") {
  component_name = "ethertap"
  deps = [ ":ethertap-driver" ]
  info = "ethertap-info.json"
  colocate = true
}

test("ethertap-unittest") {
  output_name = "ethertap-unittest"
  configs += [ "//build/config:all_source" ]
  sources = [
    "ethertap-test.cc",
    "ethertap.cc",
  ]
  deps = [
    ":ethertap_bind",
    "//sdk/banjo/fuchsia.hardware.ethernet:fuchsia.hardware.ethernet_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.ethernet:fuchsia.hardware.ethernet_c",
    "//sdk/fidl/fuchsia.hardware.ethertap:fuchsia.hardware.ethertap_c",
    "//sdk/fidl/fuchsia.hardware.ethertap:fuchsia.hardware.ethertap_llcpp",
    "//sdk/lib/fidl/cpp:cpp_base",
    "//src/devices/lib/dev-operation",
    "//src/devices/testing/mock-ddk",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fidl:fidl_base",
    "//zircon/system/ulib/fidl-utils",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/pretty",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("ethertap-unittest-package") {
  package_name = "ethertap-unittest"
  deps = [ ":ethertap-unittest" ]
}

group("tests") {
  testonly = true
  deps = [
    ":ethertap-unittest-package",
    ":ethertap_bind_test",
  ]
}
