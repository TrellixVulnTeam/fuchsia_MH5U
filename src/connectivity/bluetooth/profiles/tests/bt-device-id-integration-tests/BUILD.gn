# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_test.gni")

rustc_test("tests_bin") {
  output_name = "bt_device_id_integration_tests"
  source_root = "src/main.rs"

  deps = [
    "//sdk/fidl/fuchsia.bluetooth.bredr:fuchsia.bluetooth.bredr-rustc",
    "//sdk/fidl/fuchsia.bluetooth.deviceid:fuchsia.bluetooth.deviceid-rustc",
    "//src/connectivity/bluetooth/lib/fuchsia-bluetooth",
    "//src/connectivity/bluetooth/testing/mock-piconet-server:lib_v2",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component-test",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]

  sources = [ "src/main.rs" ]

  configs -= [ "//build/config/rust:allow_unused_results" ]
}

fuchsia_test_component("bt_device_id_integration_tests") {
  manifest = "meta/bt-device-id-integration-tests.cml"
  deps = [ ":tests_bin" ]
}

fuchsia_test_package("bt-device-id-integration-tests") {
  package_deps_for_infra_only =
      [ "//src/connectivity/bluetooth/testing/mock-piconet-server" ]
  test_components = [ ":bt_device_id_integration_tests" ]

  test_specs = {
    # The profile integration tests tend to flake if tests are run in parallel.
    parallel = 1
  }

  deps = [
    # The Device ID component.
    "//src/connectivity/bluetooth/profiles/bt-device-id:component",

    # Mock Piconet Server component.
    "//src/connectivity/bluetooth/testing/mock-piconet-server:component-v2",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":bt-device-id-integration-tests" ]
}
