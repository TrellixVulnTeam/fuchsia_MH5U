# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components/fuchsia_test_component.gni")
import("//build/components/fuchsia_test_package.gni")
import("//build/rust/rustc_test.gni")
import(
    "//src/security/pkg_test/assemblies/assemble_security_pkg_test_system.gni")

template("access_ota_blob_as_executable") {
  assert(defined(invoker.base_packages),
         "base_packages must be defined for $target_name")
  assert(defined(invoker.test_binary_name),
         "test_binary_name must be defined for $target_name")
  assert(defined(invoker.test_component_manifest),
         "test_component_manifest must be defined for $target_name")
  assert(defined(invoker.test_config),
         "test_config must be defined for $target_name")
  assert(defined(invoker.root_ssl_cert),
         "root_ssl_cert must be defined for $target_name")
  assert(defined(invoker.pkg_server_certchain),
         "pkg_server_certchain must be defined for $target_name")
  assert(defined(invoker.pkg_server_key),
         "pkg_server_key must be defined for $target_name")

  assembly_base_packages = invoker.base_packages
  test_binary_name = invoker.test_binary_name
  test_component_manifest = invoker.test_component_manifest
  root_ssl_cert = invoker.root_ssl_cert
  pkg_server_certchain = invoker.pkg_server_certchain
  pkg_server_key = invoker.pkg_server_key

  test_name = target_name

  # System assembly for base system version at start of test.
  assemble_security_pkg_test_system("${test_name}_v0") {
    test_config = invoker.test_config
    board_name = board_name
    root_ssl_cert = root_ssl_cert
    base_packages = [ "//src/security/pkg_test/assemblies/hello_world_v0:hello_world-package" ] + assembly_base_packages
    packaged_assembly_directory = "access_ota_blob_as_executable_v0"
    system_version_file = "//src/security/pkg_test/assemblies/v0_version.txt"

    # Calculated on local build from 2022-02-08:
    #
    #   v0 blobs:
    #     $ wc --bytes $(fx get-build-dir)/obj/src/security/pkg_test/tests/core_access_ota_blob_as_executable_v0/blob.blk
    #     3063808 [build-dir]/obj/src/security/pkg_test/tests/core_access_ota_blob_as_executable_v0/blob.blk
    #
    #   v1 TUF repo assets:
    #     $ wc --bytes $(find $(fx get-build-dir)/gen/src/security/pkg_test/tests/core_access_ota_blob_as_executable_v1_tuf_repo_resources.unzipped -type f | grep '/\(targets\|blobs\)/')
    #     [...]
    #     46365598 total
    #
    #   Total: 3063808 + 46365598 = 49429406
    fvm_truncate_to_length = 49429406
  }

  # System assembly for system version to OTA from package server.
  assemble_security_pkg_test_system("${test_name}_v1") {
    test_config = invoker.test_config
    board_name = board_name
    root_ssl_cert = root_ssl_cert
    base_packages = [ "//src/security/pkg_test/assemblies/hello_world_v1:hello_world-package" ] + assembly_base_packages
    packaged_assembly_directory = "access_ota_blob_as_executable_v1"
    system_version_file = "//src/security/pkg_test/assemblies/v1_version.txt"

    # Note: v1 never instantiated as base system from FVM, hence no need to
    # specify `fvm_truncate_to_length`.
  }

  # Filesystem host for base system.
  fuchsia_test_component("fshost_v0-component") {
    component_name = "fshost_v0"
    manifest = "//src/security/pkg_test/tests/access_ota_blob_as_executable/meta/fshost_v0.cml"
    deps = [ "//src/security/pkg_test/fshost:fshost-binary" ]
  }

  # Filesystem host for package server.
  fuchsia_test_component("fshost_v1-component") {
    component_name = "fshost_v1"
    manifest = "//src/security/pkg_test/tests/access_ota_blob_as_executable/meta/fshost_v1.cml"
    deps = [ "//src/security/pkg_test/fshost:fshost-binary" ]
  }

  # Fake boot arguments for base system.
  fuchsia_test_component("fake_boot_arguments_v0-component") {
    component_name = "fake_boot_arguments_v0"
    manifest = "//src/security/pkg_test/tests/access_ota_blob_as_executable/meta/fake_boot_arguments_v0.cml"
    deps = [
      "//src/security/pkg_test/fake_boot_arguments:fake_boot_arguments-binary",
    ]
  }

  fuchsia_test_component("fake_boot_arguments_v1-component") {
    component_name = "fake_boot_arguments_v1"
    manifest = "//src/security/pkg_test/tests/access_ota_blob_as_executable/meta/fake_boot_arguments_v1.cml"
    deps = [
      "//src/security/pkg_test/fake_boot_arguments:fake_boot_arguments-binary",
    ]
  }

  # Resources and component for package server configured to serve "v1" OTA.
  resource_group("${test_name}_v1-pkg_server-tls_resources") {
    testonly = true
    files = [
      {
        source = pkg_server_certchain
        dest = "data/pkg_server/tls/pkg_server.certchain"
      },
      {
        source = pkg_server_key
        dest = "data/pkg_server/tls/pkg_server.rsa"
      },
    ]
  }
  fuchsia_test_component("${test_name}_v1-pkg_server-component") {
    component_name = "pkg_server"
    manifest = "//src/security/pkg_test/tests/access_ota_blob_as_executable/meta/pkg_server.cml"
    deps = [
      ":${test_name}_v1-pkg_server-tls_resources",
      "//src/security/pkg_test/pkg_server:pkg_server-binary",

      # Defined by assemble_security_pkg_test_system("${test_name}_v1").
      ":${test_name}_v1_tuf_repo_resources",
      ":${test_name}_v1_update_package_resource",
    ]
  }

  # Resources that constitute inputs to the test component.
  resource_group("${test_name}_test-resources") {
    testonly = true
    files = [
      {
        source = invoker.test_config
        dest = "data/test_config.json5"
      },
    ]
  }

  # Top-level test component that composes its own filesystem host, SWD stack, and
  # package server for receiving OTA.
  rustc_test("${test_name}-test") {
    output_name = test_binary_name
    edition = "2018"
    source_root = "//src/security/pkg_test/tests/access_ota_blob_as_executable/src/main.rs"
    data_deps = [
      ":${test_name}_test-resources",
      ":${test_name}_v0_system_resources",
      "//src/security/pkg_test/assemblies/hello_world_v0:meta_far",
      "//src/security/pkg_test/assemblies/hello_world_v1:meta_far",
    ]
    deps = [
      "//garnet/lib/rust/files_async",
      "//garnet/lib/rust/io_util",
      "//sdk/fidl/fuchsia.io:fuchsia.io-rustc",
      "//sdk/fidl/fuchsia.mem:fuchsia.mem-rustc",
      "//sdk/fidl/fuchsia.pkg:fuchsia.pkg-rustc",
      "//sdk/fidl/fuchsia.sys2:fuchsia.sys2-rustc",
      "//src/lib/fidl/rust/fidl",
      "//src/lib/fuchsia",
      "//src/lib/fuchsia-async",
      "//src/lib/fuchsia-component",
      "//src/lib/syslog/rust:syslog",
      "//src/lib/zircon/rust:fuchsia-zircon",
      "//src/security/pkg_test/pkg_server:test.security.pkg-rustc",
      "//src/security/pkg_test/util",
      "//src/sys/pkg/fidl/fuchsia.update.installer:fuchsia.update.installer-rustc",
      "//src/sys/pkg/lib/fuchsia-hash",
      "//src/sys/pkg/lib/fuchsia-merkle",
      "//third_party/rust_crates:anyhow",
      "//third_party/rust_crates:futures",
      "//third_party/rust_crates:openat",
      "//third_party/rust_crates:regex",
    ]
    sources = [
      "//src/security/pkg_test/tests/access_ota_blob_as_executable/src/main.rs",
    ]
  }

  fuchsia_test_component("${test_name}-component") {
    component_name = test_name
    manifest = test_component_manifest
    deps = [ ":${test_name}-test" ]
  }

  fuchsia_test_package("${test_name}-package") {
    package_name = test_name
    test_components = [ ":${test_name}-component" ]
    deps = [
      # Package server configured to serve "v1" OTA update.
      ":${test_name}_v1-pkg_server-component",

      # Filesystem hosts for both test system and package server subsystem.
      ":fshost_v0-component",
      ":fshost_v1-component",

      # Fake services for update flow.
      "//src/security/pkg_test/dns_resolver:security_pkg_dns_resolver-component",
      "//src/security/pkg_test/paver:security_pkg_paver-component",
      "//src/security/pkg_test/reboot:security_pkg_reboot-component",

      # Production SWD components.
      "//src/sys/pkg/bin/pkg-cache:component",
      "//src/sys/pkg/bin/pkg-resolver:component",
      "//src/sys/pkg/bin/system-update-committer:component",
      "//src/sys/pkg/bin/system-updater:component",

      # Production network components for OTA update.
      "//src/connectivity/network/netstack:component",

      # Mock SWD component dependencies.
      ":fake_boot_arguments_v0-component",
      ":fake_boot_arguments_v1-component",
      "//src/cobalt/bin/testing/mock_cobalt:component_v2",
    ]

    # TODO(fxbug.dev/88453): Complete test dependency composition to eliminate
    # error logs:
    # - system-updater: "Failed to read vbmeta hash: while opening the file:
    #   open failed with status: NOT_FOUND: NOT_FOUND".
    test_specs = {
      log_settings = {
        max_severity = "FATAL"
      }
    }
  }
}
