# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/config.gni")

group("tests") {
  testonly = true

  deps = [
    ":factory-reset-test",

    # The deps below are necessary on minimal product configs.
    "//src/graphics/bin/vulkan_loader",
  ]
}

executable("factory_reset_test") {
  testonly = true
  sources = [ "factory_reset_test.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.media",
    "//sdk/fidl/fuchsia.media.sounds",
    "//sdk/fidl/fuchsia.recovery",
    "//sdk/fidl/fuchsia.ui.input",
    "//sdk/lib/sys/cpp/testing:integration_fixture",
    "//src/lib/fxl",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/zx",
  ]
}

config_data("factory-reset-test-audio") {
  for_pkg = "factory-reset-test"

  # This is an empty file required for MediaRetriever to startup.
  sources = [ "data/chirp-start-tone.wav" ]
}

fuchsia_unittest_package("factory-reset-test") {
  package_deps_for_infra_only = [
    "//src/cobalt/bin/testing/mock_cobalt:mock_cobalt",
    "//src/ui/bin/hardware_display_controller_provider:fake-hardware-display-controller-provider",
  ]
  manifest = "meta/factory_reset_test.cmx"
  deps = [
    ":factory-reset-test-audio",
    ":factory_reset_test",
    "//src/ui/bin/root_presenter:component_v1_for_test",
    "//src/ui/scenic:component",
  ]

  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
    environments = [
      {
        dimensions = {
          # Ensure the device has Vulkan.
          device_type = "AEMU"
        }
      },
    ]
  }
}
