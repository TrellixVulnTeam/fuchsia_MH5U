# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/flutter/config.gni")
import("//build/testing/environments.gni")

group("unit") {
  testonly = true
  public_deps = [ ":semantics_tests" ]
}

source_set("semantics_integration_test_fixture_v2") {
  testonly = true

  sources = [
    "semantics_integration_test_fixture_v2.cc",
    "semantics_integration_test_fixture_v2.h",
  ]

  deps = [
    "//sdk/lib/fdio",
    "//sdk/lib/fit",
    "//sdk/lib/sys/component/cpp/testing:cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/cpp/testing:integration_fixture",
    "//sdk/lib/ui/scenic/cpp",
    "//src/graphics/bin/vulkan_loader",
    "//src/lib/fsl",
    "//src/lib/fxl",
    "//src/lib/testing/loop_fixture",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.accessibility.semantics",
    "//sdk/fidl/fuchsia.cobalt",
    "//sdk/fidl/fuchsia.component",
    "//sdk/fidl/fuchsia.fonts",
    "//sdk/fidl/fuchsia.hardware.display",
    "//sdk/fidl/fuchsia.intl",
    "//sdk/fidl/fuchsia.kernel",
    "//sdk/fidl/fuchsia.memorypressure",
    "//sdk/fidl/fuchsia.net.interfaces",
    "//sdk/fidl/fuchsia.posix.socket",
    "//sdk/fidl/fuchsia.scheduler",
    "//sdk/fidl/fuchsia.sys",
    "//sdk/fidl/fuchsia.tracing.provider",
    "//sdk/fidl/fuchsia.ui.app",
    "//sdk/fidl/fuchsia.ui.input",
    "//sdk/fidl/fuchsia.ui.policy",
    "//sdk/fidl/fuchsia.ui.scenic",
    "//sdk/fidl/fuchsia.vulkan.loader",
    "//src/lib/testing/loop_fixture",
    "//src/ui/a11y/lib/annotation/tests/mocks",
    "//src/ui/a11y/lib/semantics",
    "//src/ui/a11y/lib/semantics/util",
    "//src/ui/a11y/lib/view",
    "//src/ui/a11y/lib/view/tests/mocks",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/async-loop",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

group("integration") {
  testonly = true
  public_deps = [
    ":semantics-integration-tests",
    "//garnet/packages/prod:scenic",
    "//src/chromium:web_engine",
    "//src/cobalt/bin/testing/mock_cobalt",
    "//src/flutter",
    "//src/fonts:downstream_test_deps",
    "//src/graphics/bin/vulkan_loader",
    "//src/ui/a11y/bin/demo:a11y-demo",
    "//src/ui/bin/hardware_display_controller_provider:fake-hardware-display-controller-provider",
    "//src/ui/bin/text:text_manager",
  ]
}

executable("semantics_tests") {
  output_name = "semantics_tests"
  testonly = true
  sources = [
    "a11y_semantics_event_manager_test.cc",
    "semantic_tree_parser.cc",
    "semantic_tree_parser.h",
    "semantic_tree_parser_test.cc",
    "semantic_tree_service_unittest.cc",
    "tree_unittest.cc",
  ]
  public_deps = [ "//sdk/fidl/fuchsia.accessibility.semantics" ]
  deps = [
    "//sdk/fidl/fuchsia.accessibility",
    "//sdk/fidl/fuchsia.sys",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/inspect/testing/cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/cpp/testing:unit",
    "//sdk/lib/syslog/cpp",
    "//sdk/lib/ui/scenic/cpp",
    "//sdk/lib/vfs/cpp",
    "//src/lib/files",
    "//src/lib/fsl",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/json_parser",
    "//src/lib/testing/loop_fixture",
    "//src/ui/a11y/bin/a11y_manager/tests/util",
    "//src/ui/a11y/lib/screen_reader/util",
    "//src/ui/a11y/lib/semantics",
    "//src/ui/a11y/lib/semantics/tests/mocks",
    "//third_party/glm",
    "//third_party/googletest:gmock",
    "//third_party/rapidjson",
  ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_component("flutter_semantics_tests") {
  testonly = true
  deps = [ ":flutter_semantics_tests_bin" ]
  manifest = "meta/flutter-semantics-tests-v2.cml"
}

fuchsia_component("web_semantics_tests") {
  testonly = true
  deps = [ ":web_semantics_tests_bin" ]
  manifest = "meta/web-semantics-tests-v2.cml"
}

resource("offscreen_node") {
  sources = [ "testdata/offscreen_node.html" ]
  outputs = [ "data/offscreen_node.html" ]
}

resource("dynamic_button") {
  sources = [ "testdata/dynamic_button.html" ]
  outputs = [ "data/dynamic_button.html" ]
}

resource("static") {
  sources = [ "testdata/static.html" ]
  outputs = [ "data/static.html" ]
}

fuchsia_test_package("semantics-integration-tests") {
  package_deps_for_infra_only = [
    "//src/chromium:web_engine",
    "//src/cobalt/bin/testing/mock_cobalt:mock_cobalt",
    "//src/fonts:pkg",
    "//src/ui/a11y/bin/demo:a11y-demo",
    "//src/ui/bin/hardware_display_controller_provider:fake-hardware-display-controller-provider",
    "//src/ui/scenic:scenic_pkg",
    flutter_default_build_cfg.runner_dep,
  ]
  test_components = [
    ":flutter_semantics_tests",
    ":web_semantics_tests",
  ]

  deps = [
    ":dynamic_button",
    ":offscreen_node",
    ":static",
    "//src/connectivity/network/netstack:component-legacy",
    "//src/ui/bin/root_presenter:component_v1_for_test",
  ]

  test_specs = {
    environments = [
      {
        dimensions = {
          device_type = "AEMU"
        }
      },
    ]
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

executable("flutter_semantics_tests_bin") {
  testonly = true

  sources = [ "flutter_semantics_tests.cc" ]

  deps = [
    ":semantics_integration_test_fixture_v2",
    "//sdk/lib/sys/component/cpp/testing:cpp",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/ui/base_view",
    "//src/ui/testing/views",
    "//third_party/googletest:gtest",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.tracing.provider",
    "//sdk/fidl/fuchsia.vulkan.loader",
    "//src/ui/a11y/bin/demo:a11y-demo",
  ]

  data_deps = [ "//src/ui/a11y/bin/demo:a11y-demo" ]
}

executable("web_semantics_tests_bin") {
  testonly = true
  sources = [ "web_semantics_tests.cc" ]
  deps = [
    ":semantics_integration_test_fixture_v2",
    "//sdk/fidl/fuchsia.cobalt",
    "//sdk/fidl/fuchsia.component",
    "//sdk/fidl/fuchsia.fonts",
    "//sdk/fidl/fuchsia.kernel",
    "//sdk/fidl/fuchsia.memorypressure",
    "//sdk/fidl/fuchsia.net.interfaces",
    "//sdk/fidl/fuchsia.netstack",
    "//sdk/fidl/fuchsia.posix.socket",
    "//sdk/fidl/fuchsia.scheduler",
    "//sdk/fidl/fuchsia.sys",
    "//sdk/fidl/fuchsia.tracing.provider",
    "//sdk/fidl/fuchsia.ui.app",
    "//sdk/fidl/fuchsia.web",
    "//sdk/lib/fit",
    "//sdk/lib/sys/component/cpp/testing:cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//sdk/lib/ui/scenic/cpp",
    "//src/chromium/web_engine/meta/shards:web_engine_feature_network",
    "//src/chromium/web_engine/meta/shards:web_engine_feature_vulkan",
    "//src/chromium/web_engine/meta/shards:web_engine_view",
    "//src/chromium/web_runner_tests:mock_server",
    "//src/lib/fxl",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/ui/base_view",
    "//src/ui/a11y/lib/view",
    "//src/ui/testing/views",
    "//third_party/googletest:gtest",
  ]
}
