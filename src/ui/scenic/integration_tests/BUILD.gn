# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//src/graphics/lib/magma/gnbuild/magma.gni")

group("integration_tests") {
  testonly = true
  public_deps = [
    ":flatland_integration_tests",
    ":gfx_integration_tests",
    ":observer_integration_tests",
    ":screenshot_integration_test",
  ]
}

source_set("common_deps") {
  testonly = true
  sources = [
    "utils.cc",
    "utils.h",
  ]
  public_deps = [
    "//sdk/fidl/fuchsia.ui.lifecycle",
    "//sdk/lib/sys/cpp/testing:integration_fixture",
    "//sdk/lib/syslog/cpp",
    "//sdk/lib/ui/scenic/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
  ]
  deps = [ "//src/lib/fostr/fidl/fuchsia.ui.input:fuchsia.ui.input" ]
}

source_set("common_deps_cts") {
  testonly = true
  sources = [
    "utils.cc",
    "utils.h",
  ]
  public_deps = [
    "//sdk/lib/sys/cpp/testing:integration_fixture",
    "//sdk/lib/syslog/cpp",
    "//sdk/lib/ui/scenic/cpp",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/zxtest",
  ]
}

source_set("scenic_realm_builder") {
  testonly = true
  sources = [
    "scenic_realm_builder.cc",
    "scenic_realm_builder.h",
  ]
  public_deps = [
    "//sdk//fidl/fuchsia.logger",
    "//sdk//fidl/fuchsia.scheduler",
    "//sdk//fidl/fuchsia.tracing.provider",
    "//sdk/fidl/fuchsia.sysmem",
    "//sdk/fidl/fuchsia.vulkan.loader",
    "//sdk/lib/sys/component/cpp/testing:cpp",
  ]
}

executable("gfx_focus_integration_test_bin") {
  testonly = true
  sources = [ "gfx_focus_integration_test.cc" ]
  deps = [
    ":common_deps",
    "//sdk/fidl/fuchsia.ui.focus",
    "//src/ui/scenic/lib/utils",
  ]
}

fuchsia_component("scenic_subrealm") {
  testonly = true
  manifest = "meta/scenic_subrealm.cml"
  deps = [
    "//examples/fidl/rust/server:echo-server",
    "//src/ui/bin/hardware_display_controller_provider:fake-hardware-display-controller-provider-cmv2-component",
    "//src/ui/scenic:component_v2",
  ]
}

fuchsia_test_component("gfx_focus_integration_test") {
  component_name = "gfx_focus_integration_test"
  manifest = "meta/gfx_focus_integration_test.cmx"
  deps = [ ":gfx_focus_integration_test_bin" ]
}

executable("gfx_legacy_coordinate_transform_test_bin") {
  testonly = true
  sources = [ "gfx_legacy_coordinate_transform_test.cc" ]
  deps = [
    ":common_deps",
    "//third_party/glm",
  ]
}

fuchsia_test_component("gfx_legacy_coordinate_transform_test") {
  component_name = "gfx_legacy_coordinate_transform_test"
  manifest = "meta/gfx_legacy_coordinate_transform_test.cmx"
  deps = [ ":gfx_legacy_coordinate_transform_test_bin" ]
}

executable("gfx_legacy_coordinate_transform_test2_bin") {
  testonly = true
  sources = [ "gfx_legacy_coordinate_transform_test2.cc" ]
  deps = [
    ":common_deps",
    "//sdk//fidl/fuchsia.ui.pointerinjector",
    "//third_party/glm",
  ]
}

fuchsia_test_component("gfx_legacy_coordinate_transform_test2") {
  component_name = "gfx_legacy_coordinate_transform_test2"
  manifest = "meta/gfx_legacy_coordinate_transform_test2.cmx"
  deps = [ ":gfx_legacy_coordinate_transform_test2_bin" ]
}

executable("gfx_startup_input_test_bin") {
  testonly = true
  sources = [ "gfx_startup_input_test.cc" ]
  deps = [
    ":common_deps",
    "//sdk/fidl/fuchsia.ui.views",
  ]
}

fuchsia_test_component("gfx_startup_input_test") {
  component_name = "gfx_startup_input_test"
  manifest = "meta/gfx_startup_input_test.cmx"
  deps = [ ":gfx_startup_input_test_bin" ]
}

executable("gfx_touch_integration_test_bin") {
  testonly = true
  sources = [ "gfx_touch_integration_test.cc" ]
  deps = [
    ":common_deps",
    "//sdk//fidl/fuchsia.ui.pointer",
    "//sdk//fidl/fuchsia.ui.pointerinjector",
    "//third_party/glm",
  ]
}

fuchsia_test_component("gfx_touch_integration_test") {
  component_name = "gfx_touch_integration_test"
  manifest = "meta/gfx_touch_integration_test.cmx"
  deps = [ ":gfx_touch_integration_test_bin" ]
}

executable("gfx_mouse_integration_test_bin") {
  testonly = true
  sources = [ "gfx_mouse_integration_test.cc" ]
  deps = [
    ":common_deps",
    "//sdk//fidl/fuchsia.ui.pointer",
    "//sdk//fidl/fuchsia.ui.pointerinjector",
    "//third_party/glm",
  ]
}

fuchsia_test_component("gfx_mouse_integration_test") {
  component_name = "gfx_mouse_integration_test"
  manifest = "meta/gfx_mouse_integration_test.cmx"
  deps = [ ":gfx_mouse_integration_test_bin" ]
}

executable("gfx_view_ref_installed_integration_test_bin") {
  testonly = true
  sources = [ "gfx_view_ref_installed_integration_test.cc" ]
  deps = [
    ":common_deps",
    "//sdk/fidl/fuchsia.ui.views",
  ]
}

fuchsia_test_component("gfx_view_ref_installed_integration_test") {
  component_name = "gfx_view_ref_installed_integration_test"
  manifest = "meta/gfx_view_ref_installed_integration_test.cmx"
  deps = [ ":gfx_view_ref_installed_integration_test_bin" ]
}

executable("flatland_touch_integration_test_bin") {
  testonly = true
  sources = [ "flatland_touch_integration_test.cc" ]
  deps = [
    ":common_deps_cts",
    ":scenic_realm_builder",
    "//sdk//fidl/fuchsia.ui.composition",
    "//sdk//fidl/fuchsia.ui.pointer",
    "//sdk//fidl/fuchsia.ui.pointerinjector",
  ]
}

fuchsia_test_component("flatland_touch_integration_test") {
  component_name = "flatland_touch_integration_test"
  manifest = "meta/flatland_touch_integration_test.cml"
  deps = [ ":flatland_touch_integration_test_bin" ]
}

executable("flatland_focus_integration_test_bin") {
  testonly = true
  sources = [ "flatland_focus_integration_test.cc" ]
  deps = [
    ":common_deps_cts",
    ":scenic_realm_builder",
    "//sdk//fidl/fuchsia.ui.composition",
    "//sdk/fidl/fuchsia.ui.focus",
  ]
}

fuchsia_test_component("flatland_focus_integration_test") {
  component_name = "flatland_focus_integration_test"
  manifest = "meta/flatland_focus_integration_test.cml"
  deps = [ ":flatland_focus_integration_test_bin" ]
}

executable("observer_registry_integration_test_bin") {
  testonly = true
  sources = [ "observer_registry_integration_test.cc" ]
  deps = [
    ":common_deps_cts",
    ":scenic_realm_builder",
    "//sdk//fidl/fuchsia.ui.composition",
    "//sdk//fidl/fuchsia.ui.focus",
    "//sdk//fidl/fuchsia.ui.observation.geometry",
    "//sdk//fidl/fuchsia.ui.observation.test",
  ]
}

fuchsia_test_component("observer_registry_integration_test") {
  component_name = "observer_registry_integration_test"
  manifest = "meta/observer_registry_integration_test.cml"
  deps = [ ":observer_registry_integration_test_bin" ]
}

executable("screenshot_integration_test_bin") {
  testonly = true
  sources = [ "screenshot_integration_test.cc" ]
  deps = [
    ":common_deps",
    ":scenic_realm_builder",
    "//sdk//fidl/fuchsia.ui.composition",
    "//src/ui/scenic/lib/allocation:allocation",
    "//src/ui/scenic/lib/screenshot:screenshot",
    "//src/ui/scenic/lib/utils:utils",
    "//zircon/system/ulib/fbl",
  ]
}

fuchsia_test_component("screenshot_integration_test") {
  component_name = "screenshot_integration_test"
  manifest = "meta/screenshot_integration_test.cml"
  deps = [ ":screenshot_integration_test_bin" ]
}

executable("flatland_mouse_integration_test_bin") {
  testonly = true
  sources = [ "flatland_mouse_integration_test.cc" ]
  deps = [
    ":common_deps_cts",
    ":scenic_realm_builder",
    "//sdk//fidl/fuchsia.ui.composition",
    "//sdk//fidl/fuchsia.ui.pointer",
    "//sdk//fidl/fuchsia.ui.pointerinjector",
  ]
}

fuchsia_test_component("flatland_mouse_integration_test") {
  component_name = "flatland_mouse_integration_test"
  manifest = "meta/flatland_mouse_integration_test.cml"
  deps = [ ":flatland_mouse_integration_test_bin" ]
}

fuchsia_test_package("gfx_integration_tests") {
  package_deps_for_infra_only = [ "//src/ui/bin/hardware_display_controller_provider:fake-hardware-display-controller-provider" ]
  test_components = [
    ":gfx_focus_integration_test",
    ":gfx_legacy_coordinate_transform_test",
    ":gfx_legacy_coordinate_transform_test2",
    ":gfx_mouse_integration_test",
    ":gfx_startup_input_test",
    ":gfx_touch_integration_test",
    ":gfx_view_ref_installed_integration_test",
  ]

  test_specs = {
    environments = [ aemu_env ] + magma_libvulkan_hardware_envs
  }
  deps = [ "//src/ui/scenic:component" ]
}

fuchsia_test_package("flatland_integration_tests") {
  test_components = [
    ":flatland_touch_integration_test",
    ":flatland_focus_integration_test",
    ":screenshot_integration_test",
    ":flatland_mouse_integration_test",
  ]
  test_specs = {
    # TODO(fxbug.dev/71344): Should have the same environments as "gfx_integration_tests" above, but
    # Flatland display compositor currently crashes on aemu, so we skip it for now.
    environments = magma_libvulkan_hardware_envs
  }
  deps = [ ":scenic_subrealm" ]
}

fuchsia_test_package("observer_integration_tests") {
  test_components = [ ":observer_registry_integration_test" ]
  test_specs = {
    environments = [ aemu_env ] + magma_libvulkan_hardware_envs
  }
  deps = [ ":scenic_subrealm" ]
}
