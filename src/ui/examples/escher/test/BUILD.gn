# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test.gni")
import("//src/lib/vulkan/build/vulkan_targets.gni")

if (is_host && !has_prebuilt_vulkan_runtime) {
  # Escher cannot run without prebuilt Vulkan runtime on host for now.
  # Since we may include Escher host tests in the default build,
  # we substitute in a dummy executable in the case of a mac build.
  test("escher_examples_test") {
    deps = [
      "//src/lib/fxl/test:gtest_main",
      "//src/ui/lib/escher/test:no_op_mac_unittest",
    ]
  }
} else {
  vulkan_test("escher_examples_test") {
    sources = [
      "rainfall_smoke_test.cc",
      "run_all_unittests.cc",
      "waterfall_smoke_test.cc",
    ]

    deps = [
      "//src/lib/fxl",
      "//src/ui/examples/escher/rainfall:rainfall_demo",
      "//src/ui/examples/escher/waterfall:waterfall_demo",
      "//src/ui/lib/escher/test/common:gtest_escher_with_runtime_glsl",
    ]

    if (is_host) {
      deps += [ "//src/ui/examples/escher/common:host_test_data" ]
    }
  }
}
