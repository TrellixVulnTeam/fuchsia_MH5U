# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

test("fvm-driver-integration") {
  output_name = "fvm-driver-integration-test"
  sources = [
    "partition_load_test.cc",
    "volume_manager_api_test.cc",
  ]

  deps = [
    "//sdk/fidl/fuchsia.device:fuchsia.device_llcpp",
    "//sdk/fidl/fuchsia.hardware.block.volume:fuchsia.hardware.block.volume_llcpp",
    "//src/devices/testing/driver-integration-test-shim",
    "//src/devices/testing/no_ddk",
    "//src/lib/storage/fs_management",
    "//src/storage/fvm:test_support",
    "//src/storage/fvm/driver:shared",
    "//zircon/system/ulib/fdio-caller",
    "//zircon/system/ulib/zxtest",
  ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]
}

fuchsia_unittest_package("fvm-driver-integration-test") {
  test_specs = {
    log_settings = {
      # Some of the API tests deliberately test the error cases that log.
      max_severity = "ERROR"
    }
  }

  deps = [
    ":fvm-driver-integration",
    "//src/devices/block/drivers/core",
    "//src/devices/block/drivers/ramdisk",
    "//src/devices/bus/drivers/platform",
    "//src/storage/fvm/driver",
  ]
}
