# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

group("tests") {
  testonly = true
  deps = [ ":virtio_sound_unittests" ]
}

rustc_binary("virtio_sound_bin") {
  name = "virtio_sound"
  with_unit_tests = true

  sources = [
    "src/audio_streams.rs",
    "src/main.rs",
    "src/notification.rs",
    "src/reply.rs",
    "src/sequencer.rs",
    "src/service.rs",
    "src/throttled_log.rs",
    "src/wire.rs",
    "src/wire_convert.rs",
  ]

  deps = [
    "//sdk/fidl/fuchsia.media:fuchsia.media-rustc",
    "//sdk/fidl/fuchsia.scheduler:fuchsia.scheduler-rustc",
    "//sdk/fidl/fuchsia.virtualization.hardware:fuchsia.virtualization.hardware-rustc",
    "//sdk/fidl/fuchsia.virtualization.hardware:fuchsia.virtualization.hardware-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-runtime",
    "//src/lib/mapped-vmo",
    "//src/lib/shared-buffer",
    "//src/lib/syslog",
    "//src/lib/zerocopy",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/virtualization/lib/machina-virtio-device",
    "//src/virtualization/lib/virtio-device",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:async-trait",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:iota",
    "//third_party/rust_crates:num-traits",
    "//third_party/rust_crates:once_cell",
    "//third_party/rust_crates:pretty_assertions",
    "//third_party/rust_crates:scopeguard",
    "//third_party/rust_crates:tokio",
    "//third_party/rust_crates:tracing",
  ]
}

fuchsia_unittest_package("virtio_sound_unittests") {
  deps = [ ":virtio_sound_bin_test" ]
}

# TODO(fxbug.dev/72386)
# CFv2 migration in progress
# v2 component is being used in device tests
# v1 component is being used in prod code
fuchsia_package("virtio_sound") {
  deps = [
    ":virtio_sound_v1",
    ":virtio_sound_v2",
  ]
}

fuchsia_component("virtio_sound_v2") {
  manifest = "../../meta/virtio_sound.cml"
  deps = [ ":virtio_sound_bin" ]
  component_name = "virtio_sound"
}

fuchsia_component("virtio_sound_v1") {
  manifest = "../../meta/virtio_sound.cmx"
  deps = [ ":virtio_sound_bin" ]
  component_name = "virtio_sound"
}
