# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/testing/environments.gni")
import("//src/chromium/web_runner_tests/generate_web_runner_tests.gni")

source_set("web_context") {
  testonly = true

  sources = [
    "web_context.cc",
    "web_context.h",
  ]

  deps = [
    "//sdk/lib/fdio",
    "//sdk/lib/syslog/cpp",
    "//third_party/googletest:gtest",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.web",
    "//sdk/lib/sys/cpp",
  ]
}

source_set("test_server") {
  testonly = true

  sources = [
    "test_server.cc",
    "test_server.h",
  ]

  deps = [
    "//sdk/lib/fit",
    "//src/lib/fxl",
  ]
}

source_set("mock_server") {
  testonly = true

  sources = [
    "mock_get.cc",
    "mock_get.h",
  ]

  public_deps = [ ":test_server" ]

  deps = [
    "//sdk/lib/syslog/cpp",
    "//src/lib/fxl",
    "//third_party/googletest:gtest",
  ]
}

executable("web_runner_integration_tests_bin") {
  testonly = true

  sources = [ "web_runner_integration_tests.cc" ]

  deps = [
    ":test_server",
    ":web_context",
    "//sdk/fidl/fuchsia.sys",
    "//sdk/lib/fit",
    "//sdk/lib/sys/cpp/testing:unit",
    "//sdk/lib/syslog/cpp",
    "//src/chromium/web_engine/meta/shards:web_engine_feature_network",
    "//src/lib/fxl",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//third_party/googletest:gtest",
  ]

  data_deps = [ "//src/fonts:downstream_test_deps($default_toolchain)" ]
}

executable("web_runner_pixel_tests_bin") {
  testonly = true

  sources = [ "web_runner_pixel_tests.cc" ]

  deps = [
    ":mock_server",
    ":web_context",
    "//sdk/fidl/fuchsia.sys",
    "//sdk/fidl/fuchsia.ui.app",
    "//sdk/fidl/fuchsia.ui.policy",
    "//sdk/fidl/fuchsia.ui.scenic",
    "//sdk/fidl/fuchsia.ui.views",
    "//sdk/lib/fit",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//sdk/lib/ui/scenic/cpp",
    "//src/chromium/web_engine/meta/shards:web_engine_feature_hardware_video_decoder",
    "//src/chromium/web_engine/meta/shards:web_engine_feature_network",
    "//src/chromium/web_engine/meta/shards:web_engine_view",
    "//src/lib/fsl",
    "//src/lib/fxl",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//src/lib/ui/base_view",
    "//src/ui/testing/views",
    "//third_party/googletest:gtest",
  ]

  data_deps = [ "//src/fonts:downstream_test_deps($default_toolchain)" ]
}

fuchsia_component("web_runner_integration_tests") {
  testonly = true
  deps = [ ":web_runner_integration_tests_bin" ]
  manifest = "meta/web_runner_integration_tests.cmx"
}

fuchsia_component("web_runner_pixel_tests") {
  testonly = true
  deps = [ ":web_runner_pixel_tests_bin" ]
  manifest = "meta/web_runner_pixel_tests.cmx"
}

resource("resources") {
  sources = [
    "//garnet/test_data/media/third_party/chromium_media_test_data/blackwhite_yuv420p.webm",
    "data/dynamic.html",
    "data/static.html",
    "data/video.html",
  ]
  outputs = [ "data/{{source_file_part}}" ]
}

fuchsia_test_package("web_runner_tests") {
  package_deps_for_infra_only = [
    "//src/chromium:web_engine",
    "//src/chromium:web_runner",
  ]

  test_components = [
    ":web_runner_integration_tests",
    # (TODO: fxbug.dev/72369) Temporary disable this test due to test flakes.
    #":web_runner_pixel_tests",
  ]

  deps = [
    ":resources",

    # Provides a fake implementation for `fuchsia.intl.ProfileProvider`.  This
    # just declares that the binary needs to be compiled, but doesn't actually
    # add the package.  That happens below in the "tests" group.
    "//src/connectivity/network/netstack:component-legacy",
    "//src/testing/fidl/intl_property_manager",
  ]

  test_specs = {
    environments = with_display_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

generate_web_runner_tests("tests") {
  chromium_dir = "//src/chromium"
}
