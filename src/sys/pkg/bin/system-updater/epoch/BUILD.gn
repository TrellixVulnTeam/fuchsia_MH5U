# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_test.gni")

if (is_host) {
  _generator_deps = [
    "//src/sys/pkg/lib/epoch",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:argh",
    "//third_party/rust_crates:serde",
    "//third_party/rust_crates:serde_json",
    "//third_party/rust_crates:thiserror",
  ]
  _generator_test_deps = [
    "//third_party/rust_crates:assert_matches",
    "//third_party/rust_crates:indoc",
    "//third_party/rust_crates:proptest",
  ]
  _generator_sources = [
    "src/args.rs",
    "src/history.rs",
    "src/main.rs",
  ]

  rustc_binary("generator") {
    name = "epoch_generator"
    edition = "2018"
    deps = _generator_deps
    sources = _generator_sources
  }

  rustc_test("epoch-generator-tests") {
    deps = _generator_deps + _generator_test_deps
    source_root = "src/main.rs"
    sources = _generator_sources
  }
}

group("host_tests") {
  testonly = true
  deps = [ ":epoch-generator-tests($host_toolchain)" ]
}
