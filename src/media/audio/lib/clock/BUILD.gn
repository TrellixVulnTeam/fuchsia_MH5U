# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be found in the LICENSE file.

# Clock-related utility functions, for audio tests, tools or drivers

import("//build/components.gni")

source_set("clock") {
  sources = [
    "audio_clock.cc",
    "audio_clock.h",
    "audio_clock_coefficients.h",
    "audio_clock_factory.h",
    "clone_mono.cc",
    "clone_mono.h",
    "pid_control.cc",
    "pid_control.h",
    "utils.cc",
    "utils.h",
  ]

  deps = [ "//sdk/lib/syslog/cpp" ]

  public_deps = [
    "//sdk/fidl/fuchsia.hardware.audio",
    "//sdk/lib/fit-promise",
    "//src/media/audio/lib/timeline",
    "//zircon/system/ulib/affine",
    "//zircon/system/ulib/zx",
  ]
}

executable("unittest_bin") {
  visibility = [ ":*" ]
  testonly = true
  output_name = "audio-libclock-unittests"

  sources = [
    "audio_clock_unittest.cc",
    "clone_mono_unittest.cc",
    "pid_control_unittest.cc",
    "utils_unittest.cc",
  ]

  deps = [
    ":clock",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/media/audio/lib/clock/testing",
    "//src/zircon/lib/zircon",
  ]
}

executable("death_unittest_bin") {
  visibility = [ ":*" ]
  testonly = true
  output_name = "audio-libclock-death-unittests"

  sources = [ "audio_clock_death_unittest.cc" ]

  deps = [
    ":clock",
    "//src/lib/fxl/test:gtest_main",
    "//src/zircon/lib/zircon",
  ]
}

fuchsia_unittest_package("audio-libclock-unittests") {
  deps = [ ":unittest_bin" ]
}

fuchsia_unittest_package("audio-libclock-death-unittests") {
  manifest = "meta/audio-libclock-death-unittests.cmx"
  deps = [ ":death_unittest_bin" ]
  test_specs = {
    log_settings = {
      max_severity = "FATAL"
    }
  }
}

group("tests") {
  testonly = true
  deps = [
    ":audio-libclock-death-unittests",
    ":audio-libclock-unittests",
    "testing:tests",
  ]
}
