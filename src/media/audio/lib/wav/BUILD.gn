# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

source_set("wav") {
  sources = [
    "wav_internal.h",
    "wav_reader.cc",
    "wav_reader.h",
    "wav_writer.cc",
    "wav_writer.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.media",
    "//sdk/lib/fdio",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fxl",
    "//zircon/system/ulib/fbl",
  ]
}

test("unittest_bin") {
  visibility = [ ":*" ]
  testonly = true
  output_name = "audio-libwav-unittests"

  sources = [ "wav_unittest.cc" ]

  deps = [
    ":wav",
    "//src/lib/fxl/test:gtest_main",
  ]
}

fuchsia_unittest_package("audio-libwav-unittests") {
  manifest = "meta/audio-libwav-unittests.cmx"
  deps = [ ":unittest_bin" ]

  test_specs = {
    environments = basic_envs
  }
}

group("tests") {
  testonly = true
  deps = [ ":audio-libwav-unittests" ]
}
