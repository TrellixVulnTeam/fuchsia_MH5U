# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")
import("//src/media/audio/hardware.gni")

group("test") {
  testonly = true

  # These tests must run in release builds because they have hard realtime constraints.
  # Debug builds are too slow.
  # TODO(fxbug.dev/65608): Split into tests that have realtime constraints and tests that do not.
  if (!is_debug) {
    deps = [
      ":audio_driver_tests",
      "//src/connectivity/bluetooth/tests/audio-device-output-harness",
    ]
  }
}

fuchsia_unittest_package("audio_driver_tests") {
  manifest = "meta/audio_driver_tests.cmx"
  deps = [ ":audio_driver_tests_bin" ]

  # This test is intended to be run on real hardware and on a virtualized Bluetooth a2dp driver.
  # It does not autostart virtual_audio but will also run/pass there if VA is already running.
  test_specs = {
    environments = hardware_envs + basic_envs
  }
}

executable("audio_driver_tests_bin") {
  testonly = true
  output_name = "audio_driver_tests"

  sources = [
    "admin_test.cc",
    "admin_test.h",
    "audio_device_enumerator_stub.cc",
    "audio_device_enumerator_stub.h",
    "basic_test.cc",
    "basic_test.h",
    "main.cc",
    "test_base.cc",
    "test_base.h",
  ]

  deps = [
    "//sdk/fidl/fuchsia.hardware.audio",
    "//sdk/fidl/fuchsia.media",
    "//sdk/lib/media/cpp:no_converters",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/cpp/testing:enclosing_environment",
    "//src/lib/fsl",
    "//src/lib/fxl/test:test_settings",
    "//src/lib/testing/loop_fixture",
    "//src/media/audio/lib/test:message_transceiver",
    "//src/media/audio/lib/test:test_fixture",
    "//zircon/system/ulib/fzl",
  ]
}
