# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

rustc_binary("actor-bin") {
  testonly = true
  name = "actor"
  edition = "2018"
  source_root = "actor/main.rs"
  deps = [
    "//sdk/fidl/fuchsia.diagnostics:fuchsia.diagnostics-rustc",
    "//src/lib/diagnostics/reader",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//src/sys/test_runners/stress_test/actor",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:rand",
  ]

  sources = [ "actor/main.rs" ]
}

resource("archivist-config-file") {
  testonly = true
  sources = [ "//src/diagnostics/archivist/configs/archivist_config.json" ]
  outputs = "data/{{source_file_part}}"
}

fuchsia_component("actor") {
  testonly = true
  manifest = "meta/actor.cml"
  deps = [ ":actor-bin" ]
}

fuchsia_component("archivist") {
  testonly = true
  manifest = "meta/archivist.cml"
  deps = [
    ":archivist-config-file",
    "//src/diagnostics/archivist:bin",
  ]
}

fuchsia_component("test") {
  testonly = true
  manifest = "meta/test.cml"
}

fuchsia_test_package("archivist-stress-test") {
  deps = [
    ":actor",
    ":archivist",
  ]
  test_components = [ ":test" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
    environments = [
      {
        dimensions = {
          device_type = "QEMU"
        }
        tags = [ "stress-tests" ]
      },
    ]
  }
}

group("stress-test") {
  testonly = true
  deps = [ ":archivist-stress-test" ]
}
