# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/fidl/fidl.gni")
import("//sdk/cts/build/cts_element.gni")
import("//sdk/cts/build/cts_source_library.gni")
import("//sdk/cts/build/cts_version.gni")
import("//sdk/cts/build/write_cts_file.gni")

# Declares a FIDL Library that can be used in CTS.
#
# An equivalent to fidl.gni, but ensures dependencies are allowed in CTS.
#
# dest (optional)
#     Location to copy this target to in the CTS archive.
#     Dest should only be specified if the target is not in //sdk/cts. If the
#     target is in //sdk/cts, it will be placed in the same path in the CTS
#     archive.
#     Type: string
template("cts_fidl") {
  assert(defined(invoker.sources), "Sources must be defined.")
  assert(!defined(invoker.deps), "All FIDL dependencies should be public.")

  invoker_deps = []
  if (defined(invoker.public_deps)) {
    invoker_deps += invoker.public_deps
  }
  if (defined(invoker.non_fidl_deps)) {
    invoker_deps += invoker.non_fidl_deps
  }

  if (invoker_deps != [] && cts_version == "") {
    verify_target = target_name + "_verify_deps"
  }
  write_file_target = target_name + "_cts_file"

  if (defined(verify_target)) {
    cts_element(verify_target) {
      deps = invoker_deps
      invoker_label = get_label_info(invoker.target_name, "label_no_toolchain")
    }
  }

  write_cts_file(write_file_target) {
  }

  fidl(target_name) {
    forward_variables_from(invoker, "*", [ "dest" ])
    if (!defined(non_fidl_deps)) {
      non_fidl_deps = []
    }

    if (defined(verify_target)) {
      non_fidl_deps += [ ":$verify_target" ]
    }
    non_fidl_deps += [ ":$write_file_target" ]
  }

  current_dir = get_label_info(":${target_name}", "dir")
  is_prebuilt_cts =
      string_replace(current_dir, "//prebuilt/cts", "") != current_dir
  if (!is_prebuilt_cts) {
    cts_source_library(target_name) {
      forward_variables_from(invoker,
                             [
                               "dest",
                               "public_deps",
                               "sources",
                               "testonly",
                             ])
    }
  }
}
