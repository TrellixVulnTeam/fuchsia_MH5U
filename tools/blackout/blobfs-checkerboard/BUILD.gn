# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

if (is_host) {
  rustc_binary("blackout-blobfs-checkerboard-host") {
    output_name = "blackout_blobfs_checkerboard_host"
    testonly = true
    edition = "2018"
    deps = [
      "//third_party/rust_crates:anyhow",
      "//third_party/rust_crates:structopt",
      "//tools/blackout/blackout-host",
    ]
    source_root = "src/host.rs"
    sources = [ "src/host.rs" ]

    with_unit_tests = true
    test_environments = [
      {
        dimensions = {
          testbed = "pwr_sherlock"
          pool = "fuchsia.tests.power-failure"
          device_type = "Sherlock"
        }
        tags = [ "power-failure-sherlock" ]
        netboot = true
      },
    ]
  }
}

rustc_binary("blackout-blobfs-checkerboard-target-bin") {
  output_name = "blackout_blobfs_checkerboard_target_bin"
  with_unit_tests = true
  testonly = true
  edition = "2018"
  deps = [
    "//garnet/lib/rust/files_async",
    "//garnet/lib/rust/io_util",
    "//src/lib/fuchsia-async",
    "//src/lib/storage/fs_management/rust:fs_management",
    "//src/sys/pkg/lib/fuchsia-merkle",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:byteorder",
    "//third_party/rust_crates:rand",
    "//third_party/rust_crates:structopt",
    "//tools/blackout/blackout-target",
  ]
  data_deps = [ "//src/storage/bin/blobfs" ]
  source_root = "src/target.rs"

  sources = [ "src/target.rs" ]
}

fuchsia_component("blackout-blobfs-checkerboard-target-component") {
  testonly = true
  deps = [ ":blackout-blobfs-checkerboard-target-bin" ]
  manifest = "meta/blackout_blobfs_checkerboard_target.cmx"
}

fuchsia_package("blackout-blobfs-checkerboard-target") {
  testonly = true
  deps = [ ":blackout-blobfs-checkerboard-target-component" ]
}

group("blobfs-checkerboard") {
  testonly = true
  public_deps = [
    ":blackout-blobfs-checkerboard-host($host_toolchain)",
    ":blackout-blobfs-checkerboard-target",
  ]
}

group("integration") {
  testonly = true
  public_deps = [
    ":blackout-blobfs-checkerboard-host_test($host_toolchain)",
    ":blackout-blobfs-checkerboard-target",
    "//garnet/packages/prod:run",
  ]
}
