# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_library.gni")
import("//build/rust/rustc_test.gni")
import("//src/storage/testing/driver_test_realm.gni")

rustc_library("blackout-target") {
  edition = "2018"
  deps = [
    "//garnet/lib/rust/files_async",
    "//garnet/lib/rust/io_util",
    "//sdk/fidl/fuchsia.device:fuchsia.device-rustc",
    "//src/lib/fuchsia-component",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:rand",
    "//third_party/rust_crates:structopt",
  ]

  sources = [
    "src/lib.rs",
    "src/static_tree.rs",
  ]
}

rustc_test("blackout-target-lib-test") {
  edition = "2018"
  deps = [
    "//garnet/lib/rust/files_async",
    "//garnet/lib/rust/io_util",
    "//sdk/fidl/fuchsia.device:fuchsia.device-rustc",
    "//src/lib/fuchsia-component",
    "//src/lib/storage/fs_management/rust:fs_management",
    "//src/lib/storage/ramdevice_client",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/storage/bin/minfs",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:rand",
    "//third_party/rust_crates:structopt",
  ]
  sources = [
    "src/lib.rs",
    "src/static_tree.rs",
  ]
}

storage_driver_test_realm_v2_component("blackout-target-test-component") {
  component_name = "blackout-target-test"
  deps = [ ":blackout-target-lib-test" ]
}

fuchsia_test_package("blackout-target-test") {
  package_deps_for_infra_only = [ "//garnet/bin/trace_manager:trace_manager" ]
  test_components = [ ":blackout-target-test-component" ]
  test_specs = {
    log_settings = {
      # Required due to no access to Cobalt for minfs
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  public_deps = [ ":blackout-target-test" ]
}
