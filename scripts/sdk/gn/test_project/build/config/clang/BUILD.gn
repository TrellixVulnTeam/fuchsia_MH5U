# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/clang/clang.gni")

# This adds the runtime deps for C++ for usage when cross compiling.
group("c++-runtime-deps") {
  data_deps = [ ":clang-runtime-libs" ]
}

copy("clang-runtime-libs") {
  if (target_cpu == "arm64") {
    arch = "aarch64"
  } else if (target_cpu == "x64") {
    arch = "x86_64"
  }
  vendor = "unknown"

  sys = target_os

  # Invoke clang directly to find the paths to each of these runtimes rather
  # than hardcoding the path directly.
  _sources = []
  runtimes = [
    "libc++.so.2.0",
    "libc++abi.so.1.0",
    "libunwind.so.1.0",
  ]
  current_target_tuple = "${arch}-${vendor}-${sys}"
  foreach(runtime, runtimes) {
    runtime_path = exec_script(
            "/usr/bin/env",
            [
              rebase_path("${clang_base_path}/bin/clang++", "", root_build_dir),
              "--target=${current_target_tuple}",
              "-print-file-name=${runtime}",
            ],
            "trim string")
    _sources += [ runtime_path ]
  }
  sources = _sources

  outputs = [ "${root_out_dir}/lib/{{source_name_part}}" ]
}
